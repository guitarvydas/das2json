swib {
  main = spaces rule+
  rule = ":" spaces rulename "=" spaces pattern+
  pattern = stringMatch | endop | cond | peekcond | cycle | rulecall

  endop = "_end" spaces

  rulename = name spaces

  rulecall = name spaces

  stringMatch = string spaces
  
  string = dq notdq* dq
  dq = "\""
  notdq =
    | "\\" any -- escaped
    | ~dq any -- raw

  cond = "[" spaces condClause+ "]" spaces
  peekcond = "[*" spaces peekCondClause+ "]" spaces
  peekCondClause = "|" spaces peekCondMatch ":" spaces action*
  condClause = "|" spaces condMatch ":" spaces action*
  condMatch =
    | string -- string
    | endop -- endop
    | "*" -- else
  peekCondMatch =
    | string -- string
    | endop -- endop
    | "*" -- else
  action = break | acceptAndAppend | pattern
  break = "_break" spaces
  acceptAndAppend = "." spaces

  cycle = "<<<" spaces pattern+ ">>>" spaces

  name = firstLetter moreLetter*
  firstLetter = letter | "_"
  moreLetter = digit | firstLetter


}

